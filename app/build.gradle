plugins {
    id 'com.android.application'
}

android {
    compileSdk 35
    namespace 'org.shano.mistralassistral'
    android.buildFeatures.buildConfig true
    defaultConfig {
        applicationId "org.shano.mistralassistral"
        minSdk 21
        targetSdk 35
        versionCode 300
        versionName project.findProperty("VERSION_NAME") ?: "3.0"
    }

    signingConfigs {
        release {
            if (project.hasProperty("MYAPP_RELEASE_STORE_FILE")) {
                storeFile file(MYAPP_RELEASE_STORE_FILE)
                storePassword MYAPP_RELEASE_STORE_PASSWORD
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword MYAPP_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Only use signing config if properties are available
            if (project.hasProperty("MYAPP_RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    lint {
        disable "MissingTranslation" // Translation is crowd-sourced, so this lint warning is useless
        abortOnError false
    }
    
    // F-Droid compatibility - ensure reproducible builds
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "mistralAssistral-${variant.name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
   implementation 'androidx.webkit:webkit:1.12.1'
}
